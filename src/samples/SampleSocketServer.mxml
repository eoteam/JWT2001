<?xml version="1.0" encoding="utf-8"?>
<s:NavigatorContent xmlns:fx="http://ns.adobe.com/mxml/2009" 
					xmlns:s="library://ns.adobe.com/flex/spark" 
					xmlns:mx="library://ns.adobe.com/flex/mx" 
					label="Socket Server Sample">
	<fx:Script>
		<![CDATA[
			import mx.controls.Alert;
			
			protected var serverSocket:ServerSocket;
			
			protected function onListen():void
			{
				try
				{
					serverSocket = new ServerSocket();
					serverSocket.addEventListener(Event.CONNECT, onSocketConnect);
					serverSocket.addEventListener(Event.CLOSE,onSocketClose);
					serverSocket.bind(Number(port.text));
					serverSocket.listen();                    
					log.text += "Listening on port " + port.text + "...\n";
					
				}
				catch (error:Error)
				{
					Alert.show("Port " + port.text + " may be in use. Enter another port number and try again.\n(" +
						error.message +")", "Error");
				}
			}
			
			protected function onSocketConnect(event:ServerSocketConnectEvent):void
			{
				var socket:Socket = event.socket;
				log.text += "Socket connection established on port " + port.text + "...\n";
				socket.addEventListener(ProgressEvent.SOCKET_DATA, onSocketDataReceived);
			}
			
			protected function onSocketDataReceived(event:ProgressEvent):void
			{
				try
				{
					var socket:Socket = event.target as Socket;
					var bytes:ByteArray = new ByteArray();
					socket.readBytes(bytes);
					log.text += ""+bytes;
					socket.flush();
				}
				catch (error:Error)
				{
					Alert.show(error.message, "Error");
				}
			}
			
			protected function onCloseClick(event:MouseEvent):void
			{
				try {
					serverSocket.close();
				}
				catch (error:Error)
				{
					Alert.show(error.message, "Error on close");
				}
			}
			
			protected function onSocketClose(e:Event):void
			{
				log.text += "Socket Closed";
			}
		]]>
	</fx:Script>
	
	<s:Panel width="95%" height="95%" horizontalCenter="0" title="Socket Client Sample" >
		<s:layout>
			<s:VerticalLayout paddingTop="8" paddingLeft="8" paddingRight="8" paddingBottom="8"/>
		</s:layout>
	
		<s:Label text="Start listening over a socket connection by clicking 'Listen'. Use the Socket Client sample to send data to the same port."/>
		<s:HGroup verticalAlign="middle">
			<s:Label text="Port:"/>
			<s:TextInput id="port" text="1235" width="50"/>
			<s:Button label="Listen" click="onListen()"/>
		</s:HGroup>
		<s:VGroup width="100%">
			<s:Label text="Log:"/>
			<s:TextArea id="log" width="80%" height="50%" editable="false"/>
			<s:Button label="Close Socket" click="onCloseClick(event)"/>	
		</s:VGroup>
	</s:Panel>
	
</s:NavigatorContent>
