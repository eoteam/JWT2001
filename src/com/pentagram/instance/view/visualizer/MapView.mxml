<?xml version="1.0" encoding="utf-8"?>
<modules:Module xmlns:fx="http://ns.adobe.com/mxml/2009" 
				xmlns:s="library://ns.adobe.com/flex/spark"  width="100%" height="100%"
				xmlns:mx="library://ns.adobe.com/flex/mx"  clipAndEnableScrolling="true"
				xmlns:modules="ws.tink.spark.modules.*" contentBackgroundColor="0xff0000" 
				implements="com.pentagram.instance.view.visualizer.interfaces.IMapView" 
				xmlns:assets="assets.*" 
				xmlns:maps="assets.maps.*" 
				xmlns:visualizer="com.pentagram.instance.view.visualizer.*"
				xmlns:layouts="com.pentagram.view.layouts.*"
				xmlns:views="com.pentagram.instance.view.visualizer.views.*"
				resize="updateSize()"  
				creationComplete="creationCompleteHandler(event)">
	
	<fx:Script>
		<![CDATA[
			import com.pentagram.instance.view.visualizer.interfaces.IRenderer;
			import com.pentagram.instance.view.visualizer.renderers.RendererInfo;
			import com.pentagram.instance.view.visualizer.renderers.RendererToolTip;
			
			import mx.collections.ArrayList;
			
			import org.cove.ape.APEngine;
			
			[Embed(source="/assets/fonts/FlamaBold.ttf", fontName="FlamaBookMx2",embedAsCFF="false",mimeType = "application/x-font")]
			private var _flamaBook:Class;
			
			import com.greensock.TweenNano;
			import com.pentagram.instance.view.visualizer.renderers.MapParticle;
			import com.pentagram.model.vo.Category;
			import com.pentagram.model.vo.Client;
			import com.pentagram.model.vo.Country;
			import com.pentagram.model.vo.DataRow;
			import com.pentagram.model.vo.Dataset;
			import com.pentagram.model.vo.Region;
			import com.pentagram.utils.CallTimer;
			
			import mx.events.FlexEvent;
			import mx.events.ResizeEvent;
			
			private const PATH:String = "assets/maps/";
			
			private var renderers:Vector.<MapParticle>;
			private var renderersDictionary:Dictionary;
			
			private var maxRadius:Number = 25;
			private var currentYear:String;
			private var opacity:Boolean = true;
			
			private var dataset:Dataset;
			private var resizeFlag:Boolean = false;
			private var firstPass:Boolean = true;
			private var _client:Client;
			private var _categories:ArrayList;
			private var _isCompare:Boolean = true;
			private var created:Boolean = false;
			
			public function get datasets():Array {
				return [null,null,dataset,null];
			}
			public function toggleMap(visible:Boolean):void {
				if(mapHolder) {
					var a:int = visible?1:0;
					TweenNano.to(mapHolder,.5,{alpha:a});
				}
			}
			public function updateSize():void {
				//holder.visible = false;
				doResize();
				resizeFlag = true;
				this.invalidateDisplayList();
			}
			public function set client(value:Client):void {
				_client  = value;
			}
			public function set categories(value:ArrayList):void {
				this._categories = value;
			}
			public function visualize(dataset:Dataset):void {	
				this.dataset = dataset;
				if(this.renderers && _client.countries.length != this.renderers.length) {
					for each(var country:Country in _client.countries.source) {
						var found:Boolean = false;
						for each(var c:MapParticle in this.renderers) {
							if(Object(c.sprite).id == country.shortname) {
								found = true;
								//break;
							}	
						}
						if(!found) {
							c = new MapParticle(holder.engine,this);//tooltip
							Object(c.sprite).id = country.shortname;						
							renderers.push(c);
							renderersDictionary[country.shortname] = c;
							holder.addParticle(c);
							c.visible = false;
						}
					}
				}
				if(!firstPass || created) {
					doVisualize();
				}
			}
			public function set isCompare(value:Boolean):void {
				_isCompare = value;
			}			
			public function resume():void {
				
			}
			public function pause():void {
				//this.holder.stop();
			}
			public function selectCountries(countries:Vector.<Object>):void {
				for each(var renderer:MapParticle in renderers) {
					if(renderer.data && countries.indexOf(renderer.data.country) != -1) {
						if(renderer.fillAlpha != 1) {
							TweenNano.to(renderer,0.5,{fillAlpha:1,onUpdate:updateRenderer,onUpdateParams:[renderer]});
							IRenderer(renderer.sprite).toggleInfo(true);
						}
					}
					else {
						if(renderer.fillAlpha != 0.2) {
							TweenNano.to(renderer,0.5,{fillAlpha:0.2,onUpdate:updateRenderer,onUpdateParams:[renderer]});
							 IRenderer(renderer.sprite).toggleInfo(false);
						}
					}
				}
			}
			public function updateMaxRadius(value:Number):void {
				this.maxRadius = value;
				update();
			}
			public function updateYear(year:String):void {
				this.currentYear = year;
				update();	
			}
			public function update():void {
				for each(var c:MapParticle in renderers) {
					var r:Number;
					var value:Number;
					
					if(c.data) {
						var content:String;
						if(dataset.id != -1) {
							if(dataset.time == 1) {
								value = c.data[currentYear];
								if(!isNaN(value) && value >= dataset.min && value <= dataset.max) {
									r = (value - dataset.min) / (dataset.max - dataset.min)* maxRadius;
									if(r < 0) trace("Negative Radius",r);
									content = generateContent(value,currentYear);
								}
								else
									r = 0;
							}
							else {
								value = c.data.value;
								if(!isNaN(value) && value >= dataset.min && c.data.value <= dataset.max) {
									r = (value - dataset.min) / (dataset.max - dataset.min) * maxRadius;
									content = generateContent(value);
								}
								else {
									r = 0;
								}
							}
						}
						else r = maxRadius;
						
						TweenNano.to(c, 1, {radius: r,onUpdate:updateRenderer,onUpdateParams:[c]});
						IRenderer(c.sprite).content = content;
						c.draw(holder.running);
					}
				}
			}
			public function selectAllCategories():void {
				var map:FXGLoader = mapHolder.getElementAt(0) as FXGLoader;
				if(map.region.fxgmap != "JWT2001_view_map_world.xml")
					loadWorldMap();
				for each(var c:MapParticle in renderers) {
					if(c && c.data) {
						c.visible = true;
					}
				}
				var timer:CallTimer = new CallTimer(adjustRenderers,null,250,1);
				timer.start();	
			}
			public function addCategory(value:Category,count:int):void {
				var region:Region = value as Region;
				var map:FXGLoader =  mapHolder.getElementAt(0) as FXGLoader
				for each(var c:MapParticle in renderers) {					
					if(c && c.data && c.data.country.region.name == region.name) {
						c.visible = true;	
						c.draw(holder.running);
					}
				}
				if(count > 1 && map.region.fxgmap != "JWT2001_view_map_world.xml")  {
					this.loadWorldMap();
				}
			}
			public function removeCategory(value:Category,count:int):void {
				var region:Region = value as Region;
				var map:FXGLoader = mapHolder.getElementAt(0) as FXGLoader;
				for each(var c:MapParticle in renderers) {			
					if(c && c.data && c.data.country.region.name == region.name) {
						c.visible = false;
						c.draw(holder.running);
					}
				}
			}
			public function selectCategory(value:Category):void {
				var existingMap:FXGLoader;
				var region:Region = value as Region;
				var map:FXGLoader;
				mapHolder.removeAllElements();
				map = new FXGLoader();
				map.addEventListener(Event.COMPLETE,handleMapLoaded);
				mapHolder.addElement(map);
				var file:File = File.applicationDirectory;
				file = file.resolvePath(PATH+region.fxgmap);
				map.load(file,region);
				map.percentHeight = 100;
				map.percentWidth = 100;
				
				for each(var c:MapParticle in renderers) {
					if(c && c.data && c.data.country.region.name != region.name) {
						c .visible = false;
					}
					if(c && c.data && c.data.country.region.name == region.name) {
						c.visible = true;
					}
				}
			}
			public function toggleOpacity(value:Number):void {
				if(mapHolder) {
					var a:int = value==1?0:1;
					opacity = value==1?false:true;
					TweenNano.to(mapHolder,.5,{alpha:a});
				}
				for each(var renderer:MapParticle in renderers)
					TweenNano.to(renderer, .5, {fillAlpha:value,onUpdate:updateRenderer,onUpdateParams:[renderer]});
				if(value != 1) 
					holder.stop();
				else 
					holder.start();
			}
			public function get viewOptions():Array {
				return [maxRadius,opacity,currentYear];
			}
			public function clearTooltips():void {
				for each(var r:MapParticle in this.renderers) {
					IRenderer(r.sprite).toggleInfo(false);
				}
			}
			public function get didVisualize():Boolean {
				return !firstPass;
			}
			public function clear():void {
				mapHolder.removeAllElements();
				while(holder.numChildren > 0){
					holder.removeChildAt(0);
				} 
			}
			protected function doVisualize():void {
				var r:Number;
				var c:MapParticle;
				var row:DataRow;
				var world:FXGLoader = mapHolder.getElementAt(0) as FXGLoader;
				var content:String = '';
				var value:Number;
				
				if(dataset) {
					for each(row in dataset.rows) {
						if(dataset.type == 1 && dataset.id != -1) {
							
							if(dataset.time == 1) {
								currentYear = dataset.years[0];
								value = row[dataset.years[0]];
								if(!isNaN(value) && value >= dataset.min && value <= dataset.max) {
									r = (value - dataset.min) / (dataset.max - dataset.min)* maxRadius;
									content =  generateContent(value,dataset.years[0]);
									if(r < 0) trace("Negative Radius",r);
								}
								else 
									r = 0;
							}
							else {
								value = row.value;
								if(!isNaN(value) && value >= dataset.min && value <= dataset.max) {
									r = (value - dataset.min) / (dataset.max - dataset.min) * maxRadius;
									content = generateContent(value);
								}
								else {
									r = 0;
								}	
							}
							
						}
						else {
							r = maxRadius;
							content = generateContent2(row);
						}
						c = renderersDictionary[row.country.shortname];
						if(c) {
							c.data = row;
							IRenderer(c.sprite).content = content;
							var region:Region;
							for each(var reg:Region in _categories.source) {
								if(c.data.country.region.name == reg.name) {
									region = reg;
								}
							}
							if(region.selected) {
								c.visible = true;
							}
							else {
								c.visible = false;
							}								
							c.radius = r;
						}
					}
					firstPass = false;
					var timer:CallTimer = new CallTimer(adjustRenderers,null,250,1);
					this.dispatchEvent(new Event('vizComplete'));
					timer.start();	
				}
			}			
			private function adjustRenderers():void {
				var visibleItems:Array = [];
				var c:MapParticle;
				var map:FXGLoader = mapHolder.getElementAt(0) as FXGLoader;
				for each(var countrySprite:Shape in map.countries) {
					c = renderersDictionary[countrySprite.name];						
					if(c)  {
						c.countrySprite = countrySprite;
						c.draw(holder.running);
						visibleItems.push(c);
					}
				}
				for each(c in renderers) {
					if(visibleItems.indexOf(c) == -1) {
						c.visible = false;
					}
				}
				updateSize();
			}
			private function updateRenderer(renderer:MapParticle):void {
				renderer.draw(holder.running);
			}
			private function doResize():void {
				var maxW:Number = 0;
				var widestSprite:Sprite;
				var widestRegion:Region;
				var i:int = 0;
				var child:FXGLoader;
				var fxgSprite:Sprite;
				var fxgRatio:Number;
				var ratio:Number;
				var sx:Number;
				var sy:Number;
				var matr:Matrix;
				var nw:Number; var nh:Number;  

				for each(var c:MapParticle in renderers) {
					c.draw(false);
				}
				
				if(mapHolder.numElements == 1) {
					child = FXGLoader(mapHolder.getElementAt(0)) as FXGLoader;
					if(!holder.running)
						TweenNano.to(child,0.5,{alpha:1});
					fxgSprite = child.fxgSprite;
					widestSprite = fxgSprite.getChildAt(0) as Sprite;
					fxgRatio = fxgSprite.width / fxgSprite.height;
					ratio =  mapHolder.width/ mapHolder.height;  
					if(ratio >= fxgRatio) {
						nh = mapHolder.height;
						nw = nh*fxgRatio;
					}
					else {
						nw = mapHolder.width;
						nh = nw/fxgRatio;
					}
					sx = nw/widestSprite.width;
					sy = nh/widestSprite.height;
					
					matr = widestSprite.transform.matrix;
					matr.scale(sx, sy);
					fxgSprite.transform.matrix = matr;
					fxgSprite.x = (child.width / 2) - (fxgSprite.width / 2 );
					fxgSprite.y = (child.height / 2) - (fxgSprite.height / 2);
				}
				if(holder.running)
					holder.start();
			}
			private function creationCompleteHandler(event:FlexEvent):void {	
				renderers = new Vector.<MapParticle>;
				renderersDictionary = new Dictionary();
//				var tooltip:RendererToolTip = new RendererToolTip();
//				this.addElement(tooltip);
//				tooltip.visible = false;		
//				var i:int = 0;
				for each(var country:Country in _client.countries.source) {
					var c:MapParticle = new MapParticle(holder.engine,this);//tooltip
					Object(c.sprite).id = country.shortname;						
					renderers.push(c);
					renderersDictionary[country.shortname] = c;
					holder.addParticle(c);
					c.visible = false;
				}
				if(!_isCompare)
					loadWorldMap();
				else {
					for each(var region:Region in _categories.source) {
						if(region.selected) {
							selectCategory(region);
							break;
						}
					}
				}
			}
			private function handleMapLoaded(event:Event):void {
				created = true;
				FXGLoader(event.target).removeEventListener(Event.COMPLETE,handleMapLoaded);
				if(firstPass) {
					doVisualize();
				}
				else {
					var timer:CallTimer = new CallTimer(adjustRenderers,null,250,1);
					timer.start();				
				}
			}
			private function loadWorldMap():void {
				if(!firstPass)
					mapHolder.removeAllElements();
				var map:FXGLoader = new FXGLoader();
				map.addEventListener(Event.COMPLETE,handleMapLoaded);
				mapHolder.addElement(map);				
				var file:File = File.applicationDirectory;
				file = file.resolvePath(PATH+"JWT2001_view_map_world.xml");
				var r:Region = new Region();
				r.coeff = 1; r.fxgmap = "JWT2001_view_map_world.xml";
				map.load(file,r);
				map.percentHeight = 100;
				map.percentWidth = 100;
			}
			override protected function updateDisplayList(w:Number, h:Number):void {
				super.updateDisplayList(w,h);
				if(resizeFlag) {
					resizeFlag = false;
					if(holder.running)
						holder.pause();
					doResize();
				}
			}
			private function generateContent(value:Number,year:String=null):String {
				var result:String = "<TextFlow xmlns='http://ns.adobe.com/textLayout/2008'><p fontFamily='FlamaBook'>";
				result += '<span color="#cccccc">'+dataset.name;
				if(year != null)
					result += '('+year+')';
				result += ': </span><span color="#ffffff">'+value.toString()+'</span>';
				result += "</p></TextFlow>"	;
				return result;
			}
			private function generateContent2(value:DataRow):String {
				var result:String = "<TextFlow xmlns='http://ns.adobe.com/textLayout/2008'><p fontFamily='FlamaBook'>";
				result += '<span color="#cccccc">Region';
				result += ': </span><span color="#ffffff">'+value.country.name+'</span>';
				result += "</p></TextFlow>";
				return result;
			}
		]]>
	</fx:Script>	
	<fx:Metadata>
		[Event(name="vizComplete",type="flash.events.Event")]
	</fx:Metadata>
	<s:Group width="100%" height="100%"  id="mapHolder"  />
	<views:PhysicsScene id="holder" width="100" height="100%"   />
</modules:Module>
